import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-android'

Properties props = new Properties()
props.load(new FileInputStream(file('signing.properties')))

dependencies {
  implementation fileTree(include: '*.jar', dir: 'libs')
  implementation fileTree(include: '*.aar', dir: 'libs')
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  implementation "androidx.navigation:navigation-fragment-ktx:2.7.0"
  implementation "androidx.navigation:navigation-ui-ktx:2.7.0"
  implementation 'androidx.preference:preference-ktx:1.2.1'
  implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
  implementation 'com.android.billingclient:billing:6.0.1'
  implementation 'com.google.android.gms:play-services-auth:20.6.0'
  implementation 'com.google.android.gms:play-services-base:18.2.0'
  implementation 'com.google.android.gms:play-services-safetynet:18.0.1'
  implementation 'com.google.android.material:material:1.11.0-alpha02'
  implementation 'com.google.code.gson:gson:2.9.0'
  implementation 'com.google.firebase:firebase-analytics'
  implementation platform('com.google.firebase:firebase-bom:26.1.0')
  implementation "com.google.firebase:firebase-messaging:23.2.1"
  implementation 'com.google.guava:guava:24.1-android'
  implementation 'com.google.protobuf:protobuf-java:3.19.3'
  implementation 'com.squareup.picasso:picasso:2.71828'
  implementation 'com.squareup.okhttp3:okhttp:4.9.3'
  implementation 'joda-time:joda-time:2.9.9'
  implementation project(':common')
  implementation project(':planet-render')
}

android {
    // TODO: remove the dependency on this.
  useLibrary 'org.apache.http.legacy'

  java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(11))
  }

  defaultConfig {
    compileSdk 34
    multiDexEnabled true
    minSdkVersion 21
    targetSdkVersion 34
    vectorDrawables.useSupportLibrary = true
  }

  packagingOptions {
    // Don't include NOTICE.txt or LICENSE.txt, many libraries define both and the duplicates
    // confuse the build process.
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/LICENSE.txt'
  }

  dexOptions {
    javaMaxHeapSize "2g"
  }

  signingConfigs {
    release {
      storeFile file(props['keystore'])
      storePassword props['password']
      keyAlias props['alias']
      keyPassword props['password']
    }
    debug {
      storeFile file('debug.keystore')
      storePassword 'warworlds'
      keyAlias 'Codeka'
      keyPassword 'warworlds'
    }
  }

  configurations {
    // This seems like a weird bug... something else includes listenablefuture from guava.
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
  }

  buildTypes {
    debug {
      debuggable true
      manifestPlaceholders = [applicationLabel: "@string/app_name_debug"]
      applicationIdSuffix ".debug"
      signingConfig signingConfigs.debug
    }
    release {
      manifestPlaceholders = [applicationLabel: "@string/app_name"]
      signingConfig signingConfigs.release
    }
  }
    buildFeatures {
        aidl true
    }
    namespace 'au.com.codeka.warworlds'
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
  buildToolsVersion '34.0.0'
}

// This task copies the buildings.xml and ships.xml file from the server directory so that we don't
// need to maintain two identical files separately.
task('copyBuildingShipXml') {
  doLast {
    file('src/main/assets/buildings.xml').text = "<!-- THIS FILE IS AUTO-GENERATED! EDIT THE FILE IN server/data/designs -->\n\n" +
            file('../server/data/designs/buildings.xml').getText()
    file('src/main/assets/ships.xml').text = "<!-- THIS FILE IS AUTO-GENERATED! EDIT THE FILE IN server/data/designs -->\n\n" +
            file('../server/data/designs/ships.xml').getText()
  }
}

// This is a task which will increment the build # in AndroidManifest.xml
task('incrementVersionCode') {
  doLast {
    def manifestFile = file("src/main/AndroidManifest.xml")

    // First do the versionCode element, increment the new build #
    def pattern = Pattern.compile("versionCode=\"(\\d+)\"")
    def manifestText = manifestFile.getText()
    def matcher = pattern.matcher(manifestText)
    matcher.find()

    def versionCode = Integer.parseInt(matcher.group(1)) + 1
    manifestText = matcher.replaceAll("versionCode=\"" + versionCode + "\"")

    // Now do versionName with the same build #.
    pattern = Pattern.compile("versionName=\"([0-9]+)\\.([0-9]+)\\.[0-9]+\"")
    matcher = pattern.matcher(manifestText)
    manifestText = matcher.replaceAll("versionName=\"\$1.\$2." + versionCode + "\"")

    manifestFile.write(manifestText)
  }
}

// This is a task which makes sure the warworlds.properties file is sanitized for release builds
task('resetReleaseWarworldsPropertiesFile') {
  doLast {
    def warworldsPropertiesFile = file("src/main/assets/warworlds.properties")
    def content = warworldsPropertiesFile.getText()

    // make sure on_behalf_of is commented out
    content = content.replaceAll("#?user.on_behalf_of\\s*=.*", "#user.on_behalf_of=nobody")

    // make sure debug is set to 'false'
    content = content.replaceAll("debug\\s*=.*", "debug=false")

    // change all of the in-app purchase fields to their defaults
    content = content.replaceAll("iap\\.([^=]+)=.*", "iap.\$1=\$1")

    // make sure the welcome.rss field is pointing to the live server
    content = content.replaceAll("welcome.rss=.*\$",
        "welcome.rss=http://www.war-worlds.com/forum/announcements/rss")

    warworldsPropertiesFile.write(content)
  }
}

// This is a task which makes sure the warworlds.properties file is sanitized for debug builds
task('resetDebugWarworldsPropertiesFile') {
  doLast {
    def warworldsPropertiesFile = file("src/main/assets/warworlds.properties")
    def content = warworldsPropertiesFile.getText()

    // make sure debug is set to 'true'
    content = content.replaceAll("debug\\s*=.*", "debug=true")

    warworldsPropertiesFile.write(content)
  }
}

tasks.whenTaskAdded { task ->
  if (task.name == 'generateReleaseBuildConfig') {
    task.dependsOn 'incrementVersionCode'
    task.dependsOn 'resetReleaseWarworldsPropertiesFile'
  } else if (task.name == 'generateDebugBuildConfig') {
    task.dependsOn 'resetDebugWarworldsPropertiesFile'
  }
}

preBuild.dependsOn 'copyBuildingShipXml'